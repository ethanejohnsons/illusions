plugins {
	id 'fabric-loom' version "$loom_version"
}

version = mod_version + "+" + minecraft_version
group = maven_group

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
		it.options.release = targetJavaVersion
	}
}

java {
	def javaVersion = JavaVersion.toVersion(targetJavaVersion)
	if (JavaVersion.current() < javaVersion) {
		toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
	}
	archivesBaseName = project.archives_base_name
	withSourcesJar()
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

loom {
	accessWidenerPath = file("src/main/resources/illusions.accessWidener")
}

repositories {
	maven {
		name = 'Ladysnake Mods'
		url = 'https://maven.ladysnake.org/releases'
		content {
			includeGroup 'io.github.ladysnake'
			includeGroup 'org.ladysnake'
			includeGroupByRegex 'dev\\.onyxstudios.*'
		}
	}
	maven {
		name "modrinth"
		url "https://api.modrinth.com/maven"
	}
	maven {
		name "jitpack"
		url "https://jitpack.io"
	}
}

dependencies {
	minecraft "com.mojang:minecraft:$minecraft_version"

	// Fabric
	mappings "net.fabricmc:yarn:$yarn_mappings:v2"
	modImplementation "net.fabricmc:fabric-loader:$loader_version"
	modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_version"

	// Misc
	modImplementation(include("org.ladysnake:satin:$satin_version"))

	include(implementation(annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-fabric:${mixin_squared_version}")))

	modRuntimeOnly "maven.modrinth:modmenu:$modmenu_version"
	modImplementation "maven.modrinth:sodium:$sodium_version"
	modImplementation "maven.modrinth:iris:$iris_version"

	modImplementation("org.antlr:antlr4-runtime:4.13.1")
	modImplementation("io.github.douira:glsl-transformer:2.0.1")
	modImplementation("org.anarres:jcpp:1.4.14")

	implementation("com.google.code.findbugs:jsr305:3.0.1")
}